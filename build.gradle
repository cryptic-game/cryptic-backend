plugins {
    id('base')
    id('io.freefair.git-version') version('5.2.1')
    id('io.freefair.aggregate-javadoc') version('5.2.1')
    id('io.freefair.javadoc-utf-8') version('5.2.1')
    id('io.freefair.lombok') version('5.2.1') apply(false)
    id('com.github.spotbugs') version('4.6.0') apply(false)
}

allprojects {
    group 'net.cryptic-game'
    version version.toString().startsWith('v') ? version.toString().substring(1) : version
}

subprojects {
    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/test.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"
    apply from: "${rootDir}/gradle/spotbugs.gradle"
    apply from: "${rootDir}/gradle/pmd.gradle"
    apply from: "${rootDir}/gradle/javadoc.gradle"
}

task copyJavaDocStyles(type: Copy) {
    from "$rootDir/gradle/javadoc.css"
    rename 'javadoc.css', 'stylesheet.css'
    into "$buildDir/docs/aggregateJavadoc/"
}

aggregateJavadoc {
    finalizedBy copyJavaDocStyles
}
